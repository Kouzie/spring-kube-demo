plugins {
    id 'org.springframework.boot' apply false
    id 'io.spring.dependency-management' apply false
    id 'com.google.cloud.tools.jib' apply false
}

subprojects {
    apply plugin: 'org.springframework.boot'
    apply plugin: 'io.spring.dependency-management'
    apply plugin: 'com.google.cloud.tools.jib'

    jar.enabled = false

    def url = project.properties["registryUrl"].toString()
    def username = project.properties["registryUsername"].toString()
    def password = project.properties["registryPassword"].toString()

    jib {
        from {
            image = 'docker.io/library/openjdk:17'
        }
        to {
            image = "${url}/${project.name}"
            tags = ["latest"]
            setAllowInsecureRegistries(true) // docker registry 사설 인증서 허용
            auth.username = username
            auth.password = password
        }
    }

    dependencyManagement {
        imports {
            mavenBom "org.springframework.cloud:spring-cloud-dependencies:2022.0.5"
            mavenBom "io.opentelemetry:opentelemetry-bom:1.34.1"
        }
    }

    dependencies {
        implementation project(':common')
        implementation project(':monitoring')

        implementation "io.opentelemetry:opentelemetry-sdk"
        implementation "io.opentelemetry:opentelemetry-exporter-otlp"
        implementation 'org.springframework.boot:spring-boot-starter-web'
        implementation 'org.springframework.boot:spring-boot-starter-validation'
        implementation 'org.springframework.cloud:spring-cloud-starter-openfeign'

        testImplementation 'org.springframework.boot:spring-boot-starter-test'
    }

    test {
        useJUnitPlatform()
    }

    task buildDockerImage(type: Exec) {
        print "project dir:${project.projectDir}"
        workingDir project.projectDir
        commandLine "docker", "build", "-t", project.name, "."
    }

    buildDockerImage.dependsOn build
}